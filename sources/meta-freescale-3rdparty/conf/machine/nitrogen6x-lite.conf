#@TYPE: Machine
#@NAME: Boundary Devices Nitrogen6X Lite
#@SOC: i.MX6S
#@DESCRIPTION: Machine configuration for Boundary Devices Nitrogen6X Lite
#@MAINTAINER: Ian Coolidge <ian@boundarydevices.com>

MACHINEOVERRIDES =. "mx6:mx6dl:"

include conf/machine/include/imx-base.inc
include conf/machine/include/tune-cortexa9.inc

KERNEL_DEVICETREE = "imx6dl-nit6xlite.dtb"
KERNEL_IMAGETYPE = "zImage"

PREFERRED_PROVIDER_u-boot = "u-boot-boundary"
PREFERRED_PROVIDER_virtual/kernel ?= "linux-boundary"

# Use SPI NOR U-Boot by default
IMAGE_BOOTLOADER ?= ""

# Ensure boot scripts will be available at rootfs time
do_rootfs[depends] += "u-boot-script-boundary:do_deploy"

# Boot scripts to install
BOOT_SCRIPTS = "6x_bootscript-${MACHINE}:6x_bootscript"

UBOOT_MACHINE ?= "nit6xlite_config"

SERIAL_CONSOLE = "115200 ttymxc1"

MACHINE_EXTRA_RRECOMMENDS += " bcm4330-nvram-config"
MACHINE_FEATURES += " pci wifi bluetooth"
